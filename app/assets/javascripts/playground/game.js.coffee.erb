board = undefined
game = new Chess()
statusEl = $('#status')

time = 15*60
fischer = 10

normalize = (value) ->
  seconds = value%60
  minutes = (value - seconds)/60
  if (value%60 < 10) 
    seconds = "0" + value%60
  return "<b>" + minutes + ":" + seconds + "</b>"

time_is_up = ->
  statusEl.html("<%= I18n.t 'interface.playground.game.statuses.time' %>")
  board.destroy()
  new_cfg = 
    draggable: false
    position: game.fen()
    pieceTheme: '/assets/playground/{piece}.png'
  board = new ChessBoard('board', new_cfg)

class Timer
  constructor : (@panel) ->
    @time = time
    @update()

  update : =>
    @panel.html(normalize(@time))

  start : =>
    @countdown = setInterval(@decrease, 1000)
    return

  decrease : =>
    @time = @time - 1
    @update()
    if (@time == 0)
      clearInterval(@countdown)
      time_is_up()
    return

  pause : =>
    clearInterval(@countdown)
    @time += fischer
    @update()
    return

white = new Timer($('#white_clock'))
black = new Timer($('#black_clock'))

onDragStart = (source, piece, position, orientation) ->
  if (game.game_over() == true ||
    game.turn() == 'w' && piece.search(/^b/) != -1 ||
    game.turn() == 'b' && piece.search(/^w/) != -1)
      return false

onDrop = (source, target) ->
  # see if the move is legal
  move = game.move(
    from: source,
    to: target,
    promotion: 'q'
  )

  # illegal move
  if move == null
    return 'snapback'

  updateStatus()
  updateMove(move.san)
  changeClock()

# update the board position after the piece snap
# for castling, en passant, pawn promotion
onSnapEnd = ->
  board.position(game.fen())

updateMove = (move) ->
    console.log(move)
    console.log(game.fen())

changeClock = ->
  if game.turn() == 'b'
    white.pause()
    black.start()
  else if game.turn() == 'w'
    black.pause()
    white.start()

updateStatus = ->
  status = ''

  moveColor = "<%= I18n.t 'interface.playground.white' %>"
  if game.turn() == 'b'
    moveColor = "<%= I18n.t 'interface.playground.black' %>"

  # checkmate?
  if game.in_checkmate() == true
    status = moveColor + "<%= I18n.t 'interface.playground.game.statuses.checkmate' %>"

  # draw?
  else if game.in_draw() == true
    status = "<%= I18n.t 'interface.playground.game.statuses.draw' %>"

  # game still on
  else
    status = moveColor + "<%= I18n.t 'interface.playground.to_move' %>"

    # check?
    if game.in_check() == true
      status += "<%= I18n.t 'interface.playground.game.statuses.check' %>"

  statusEl.html(status)

cfg =
  draggable: true
  position: 'start'
  onDragStart: onDragStart
  onDrop: onDrop
  onSnapEnd: onSnapEnd
  pieceTheme: '/assets/playground/{piece}.png'

board = new ChessBoard('board', cfg)

updateStatus()